use std::io::{ print };

use super::lexer::{ Token, TokenType };

enum AST {
    Null,
    True,
    False,
    Number(number),
    String(string),
    Array(Array<AST>),
    Object(Array<AST>),
    Property(string, AST),
}

struct Parser {
    current: number;
    tokens: Array<Token>;
}

impl Parser {
    fun new(tokens: Array<Token>): Parser {
        return Parser {
            tokens,
            current: 0
        };
    }

    fun peek(self: Self): Result<Token, string> {
        self.tokens.at(self.current)
    }

    fun advance(self: Self): Result<Token, string> {
        let token = self.tokens.at(self.current);
        self.current += 1;
        token
    }

    fun check(self: Self, token_type: TokenType): bool {
        if let Ok(token) = (self.peek()) {
            if token.token_type == token_type {
                return true;
            }
        }
        
        return false;
    }

    fun match_token(self: Self, ...types: Array<TokenType>): bool {
        for t in types {
            if self.check(t) {
                self.advance();
                return true;
            }
        }        
        
        return false;
    }

    fun parse(self: Self): Result<AST, string> {
        self.value()
    }

    fun value(self: Self): Result<AST, string> {
        return if let Ok(token) = self.peek() {
            match token.token_type {
                TokenType::LeftBrace => self.object(),
                TokenType::LeftBracket => self.array(),
                TokenType::Null => {
                    self.advance();
                    Ok(AST::Null)
                },
                TokenType::True => {
                    self.advance();
                    Ok(AST::True)
                },
                TokenType::False => {
                    self.advance();
                    Ok(AST::False)
                },
                TokenType::Number(n) => {
                    self.advance();
                    Ok(AST::Number(n.parse().unwrap()))
                },
                TokenType::String(s) => {
                    self.advance();
                    Ok(AST::String(s))
                },
                _ => Err("Expected a value")
            }
        };
    }

    fun object(self: Self): Result<AST, string> {
        let properties: Array<AST> = [];
        self.advance();

        while true {
            let key = if let Ok(token) = (self.advance()) {
                match token.token_type {
                    TokenType::String(s) => s,
                    _ => return Err("Expected a string key")
                }
            };

            if self.match_token(TokenType::Colon) != true {
                return Err("Expected ':'");
            }

            match self.value() {
                Ok(value) => properties.push(AST::Property(key, value)),
                Err(err) => return Err(err);
            }

            if self.match_token(TokenType::Comma) != true {
                break;
            }
        }

        if self.match_token(TokenType::RightBrace) != true {
            return Err("Expected '}'");
        }

        Ok(AST::Object(properties))
    }

    fun array(self: Self): Result<AST, string> {
        let elements: Array<AST> = [];
        self.advance();

        while true {      
            match self.value() {
                Ok(val) => elements.push(val),
                Err(err) => return Err(err);
            }

            if self.match_token(TokenType::Comma) != true {
                break;
            }
        }

        if self.match_token(TokenType::RightBracket) != true {
            return Err("Expected ']'");
        }

        Ok(AST::Array(elements))
    }
}